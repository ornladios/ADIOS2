cmake_minimum_required(VERSION 3.5)

include(CMakeFindDependencyMacro)

set(ADIOS2_HAVE_C @ADIOS2_HAVE_C@)
set(ADIOS2_HAVE_Fortran @ADIOS2_HAVE_Fortran@)
set(ADIOS2_HAVE_Python @ADIOS2_HAVE_Python@)

set(ADIOS2_HAVE_MPI @ADIOS2_HAVE_MPI@)
if(ADIOS2_HAVE_MPI)
  find_dependency(MPI)
endif()

if(NOT @BUILD_SHARED_LIBS@)
  set(ADIOS2_HAVE_BZip2 @ADIOS2_HAVE_BZip2@)
  if(ADIOS2_HAVE_BZip2)
    find_dependency(BZip2)
  endif()

  set(ADIOS2_HAVE_ZFP @ADIOS2_HAVE_ZFP@)
  if(ADIOS2_HAVE_ZFP)
    find_dependency(ZFP)
  endif()

  set(ADIOS2_HAVE_ZeroMQ @ADIOS2_HAVE_ZeroMQ@)
  if(ADIOS2_HAVE_ZeroMQ)
    find_dependency(ZeroMQ)
  endif()

  set(ADIOS2_HAVE_HDF5 @ADIOS2_HAVE_HDF5@)
  if(ADIOS2_HAVE_HDF5)
    find_dependency(HDF5 COMPONENTS C)
  endif()

  set(ADIOS2_HAVE_SST @ADIOS2_HAVE_SST@)
  if(ADIOS2_HAVE_SST)
    find_dependency(EVPath COMPONENTS C)
  endif()

  set(ADIOS2_HAVE_ADIOS1 @ADIOS2_HAVE_ADIOS1@)
  if(ADIOS2_HAVE_ADIOS1)
    if(ADIOS2_HAVE_MPI)
      find_dependency(ADIOS1 1.12.0)
    else()
      find_dependency(ADIOS1 1.12.0 sequential)
    endif()
  endif()
endif()

include("${CMAKE_CURRENT_LIST_DIR}/ADIOS2ConfigVersion.cmake")

include(FindPackageHandleStandardArgs)
set(ADIOS2_CONFIG "${CMAKE_CURRENT_LIST_FILE}")
find_package_handle_standard_args(ADIOS2 DEFAULT_MESSAGE ADIOS2_VERSION)

if(NOT TARGET adios2::adios2)
  include("${CMAKE_CURRENT_LIST_DIR}/ADIOS2Targets.cmake")
endif()
